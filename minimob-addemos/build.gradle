buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

//get parameters from local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def property_MinimobAndroidKey_KeyPassword = properties.getProperty('MinimobAndroidKey.keyPassword')
def property_MinimobAndroidKey_StorePassword = properties.getProperty('MinimobAndroidKey.storePassword')
def property_versionCode = properties.getProperty('versionCode')
def property_versionName = properties.getProperty('versionName')

android {
    signingConfigs {
        MinimobAndroidKey {
            keyAlias 'MinimobAndroidKey'
            keyPassword property_MinimobAndroidKey_KeyPassword
            storeFile file('./minimobAndroidKeyStore.jks')
            storePassword property_MinimobAndroidKey_StorePassword
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    dexOptions {
        javaMaxHeapSize "3g"
    }
    defaultConfig {
        applicationId 'com.minimob.addemos'
        minSdkVersion 16
        targetSdkVersion 23
        versionCode java.lang.Integer.parseInt(property_versionCode)
        versionName property_versionName

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.MinimobAndroidKey
        }
    }
    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 16
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:multidex:1.0.1'

    //from local copy
    compile project(':minimob-adserving')

//    //from GitHub
//    compile 'com.github.minimob:video-ad-serving:1.0.28'
}